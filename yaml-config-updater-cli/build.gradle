import java.text.SimpleDateFormat

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.palantir.graal" version "0.12.0"
}

description = "Command line executor for yaml config updater"

dependencies {
    implementation project(':yaml-config-updater')
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}",
                             "-Aother.resource.patterns=META-INF/VERSION"]
}

// custom version file is required because native image would override MANIFEST.MF
task generateVersionFile {
    inputs.property('source', "${ -> project.version} (${new SimpleDateFormat('dd MMMM YYYY', Locale.ENGLISH).format(new Date())})")
    outputs.file "$project.buildDir/version/VERSION"
    doLast {
        File file = outputs.files.singleFile
        if (file.exists()) {
            file.delete()
        } else {
            file.parentFile.mkdirs()
        }
        file << inputs.properties.get('source')
    }
}

model {
    tasks.jar {
        into("META-INF/") {
            from generateVersionFile
        }
    }
}

shadowJar {
    into("META-INF/") {
        from generateVersionFile
    }
}

jar.manifest.attributes 'Main-Class': 'ru.vyarus.yaml.updater.cli.UpdateConfigCli'

assemble.dependsOn = [shadowJar]

// not used during released
graal {
    graalVersion '21.3.0'
    javaVersion '11'
    mainClass  'ru.vyarus.yaml.updater.cli.UpdateConfigCli'
    outputName project.name

    // help graal to properly detect VS on windows 10
    windowsVsVersion '2022'
    windowsVsEdition 'Enterprise'
}